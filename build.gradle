buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        mavenLocal()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.7.4"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.42.0"
        classpath "com.gorylenko.gradle-git-properties:gradle-git-properties:2.4.1"
    }
}

def writeGitProperties = findProperty('write.gitproperties') ?: 'false'
if (writeGitProperties == 'true') {
    apply plugin: 'com.gorylenko.gradle-git-properties'
}

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'project-report'
apply plugin: 'com.github.ben-manes.versions'


apply from: 'dependencies.gradle'

defaultTasks 'clean', 'check'


archivesBaseName = 'edison-jobtrigger'
group = 'de.otto.edison'
version = '2.3.9-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    mavenLocal()
}


configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "log4j", module: "log4j"
    exclude module: "spring-boot-starter-tomcat"
}

dependencies {
    implementation libraries.edison
    implementation libraries.bootstrap
    implementation libraries.guava
    implementation libraries.logging
    implementation libraries.jetty
    implementation libraries.asyncHttp
    implementation libraries.gson
    implementation libraries.spring
    implementation libraries.aws

    testImplementation libraries.test
}

test {
    // pass -Dspring.profiles.active=local and other system properties to the test task
    systemProperties = System.properties
}

eclipse {
    classpath {
        //customizing the classes output directory:
        defaultOutputDir = file('build/bin')

        //default settings for downloading sources and Javadoc:
        downloadSources = true
        downloadJavadoc = false
    }
}

tasks.processResources.doLast {
    def resourcesDir = project.sourceSets.main.output.resourcesDir
    resourcesDir.mkdirs()

    def versionProperties = new File(resourcesDir, "version.properties")
    if (versionProperties) {
        def commit = getCommitHash()
        def version = getVersion()
        versionProperties.text = "\nedison.status.vcs.commit = ${commit}\nedison.status.vcs.version = ${version}" + "\nedison.status.vcs.url-template = https://github.com/otto-de/edison-jobtrigger"
    }
}

def String runCommand(def command) {
    Process proc = command.execute()
    def out = new StringBuffer()
    proc.consumeProcessOutputStream(out)
    proc.consumeProcessErrorStream(out)
    proc.waitFor()
    def errorlevel = proc.exitValue()
    if (errorlevel != 0) {
        throw new RuntimeException("exec failed on command: '${command}' with errorlevel ${errorlevel}".toString())
    }
    out.toString().trim()
}

def String getVersion() {
    def env = System.getenv()
    return env.VERSION ? env.VERSION : '0.1-localbuild'
}

def String getCommitHash() {
    def env = System.getenv()
    return env.COMMIT ? env.COMMIT : runCommand("git log -n 1 --format=format:%H")
}
